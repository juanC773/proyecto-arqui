//
// Install the gradle Ice Builder plug-in from the plug-in portal
//
plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.4.7' apply false
}
  
subprojects {
    //
    // Apply Java and Ice Builder plug-ins to all sub-projects
    //
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'
    apply plugin: 'application' // Agregado para habilitar la tarea `run`
 
    //
    // Both Client and Server projects share the Printer.ice Slice definitions
    //
    slice {
        java {
            files = [file("../Printer.ice")]
        }
    }
 
    //
    // Use Ice JAR files from maven central repository
    //
    repositories {
        mavenCentral()
    }
 
    //
    // Both Client and Server depend only on Ice JAR
    //
    dependencies {
        implementation 'com.zeroc:ice:3.7.6'
        implementation 'com.zeroc:icegrid:3.7.6'
    }

    //
    // Create a JAR file with the appropriate Main-Class and Class-Path attributes
    //
    jar {
        manifest {
            attributes(
                    "Main-Class": project.name == 'server' ? 'Server' : 'Client', // Ajusta el Main-Class din√°micamente
                    "Class-Path": configurations.runtimeClasspath.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }

    //
    // Define the mainClass for each subproject (used by the `application` plugin)
    //
    application {
        mainClass = project.name == 'server' ? 'Server' : 'Client'
    }
}
